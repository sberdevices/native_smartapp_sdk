apply plugin: 'maven'
apply plugin: 'signing'

apply from: 'versions.gradle'
apply from: 'publication.gradle'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.32"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
    }
}

repositories {
    maven {
        name = "OSSRH"
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

static boolean isAndroidPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["com.android.build.gradle.LibraryPlugin",
                                        "com.android.build.gradle.AppPlugin"]
}

static boolean isJavaLibraryPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["org.gradle.api.plugins.JavaPlugin",
                                        "org.gradle.api.plugins.JavaLibraryPlugin"]
}

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if (isAndroidPlugin(plugin)) {
            project.apply from: "$rootProject.projectDir/android_subproject.gradle"
        }
        if (isJavaLibraryPlugin(plugin)) {
            project.apply from: "$rootProject.projectDir/java_subproject.gradle"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        moduleName project.path.removePrefix(":").replace(':', '-')
    }
}
