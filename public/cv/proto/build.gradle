plugins {
    id 'com.android.library'
    id 'com.google.protobuf'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 29
    buildToolsVersion  "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        buildConfigField 'String', 'CV_API_VERSION', "\"3.0.0\""
        versionName "3.0.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->

            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.2.0"
    api "com.google.protobuf:protobuf-lite:3.0.1"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

            publications {
                mavenCvProto(MavenPublication) {
                    from components.release

                    groupId = rootProject.ext.pomGroupID+'.cv'
                    artifactId = "proto"
                    version = android.defaultConfig.versionName

                    pom {
                        name = "cv:proto"
                        description = 'protos used for parcelling of entities'
                        url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        licenses {
                            license {
                                name = 'Sber Public License at-nc-sa v.2'
                                url = 'https://github.com/sberdevices/salutejs/blob/master/LICENSE.txt'
                            }
                        }
                        developers {
                            developer {
                                name = 'Dmitry Levshunov'
                                email = 'Levshunov.D.A@sberbank.ru'
                            }
                            developer {
                                name = 'Irina Karpenko'
                                email = 'IViKarpenko@sberbank.ru'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/sberdevices/native_smartapp_sdk.git'
                            developerConnection = 'scm:git:ssh://github.com:sberdevices/native_smartapp_sdk.git'
                            url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        }
                    }

                    versionMapping {
                        allVariants {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenCvProto
    }
}
