plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        buildConfigField 'String', 'CV_API_VERSION', "\"3.0.0\""
        versionName "3.0.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':public:common:binderhelper')
    implementation project(':public:common:logger')
    implementation project(':public:cv:aidl')
    api project(':public:cv:api')
    api project(':public:cv:entity')
    implementation project(':public:cv:proto')
    api project(':public:cv:util')

    implementation "androidx.annotation:annotation:$versions.annotation"
    implementation "com.google.protobuf:protobuf-lite:$versions.protobuf_lite"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinxCoroutines"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = publication.deployUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

            publications {
                mavenCvImpl(MavenPublication) {
                    from components.release

                    groupId = rootProject.ext.pomGroupID+'.cv'
                    artifactId = "impl"
                    version = android.defaultConfig.versionName

                    pom {
                        name = "cv:impl"
                        description = 'cv api binding implementation, which connects to service by aidl'
                        url = publication.githubUrl
                        licenses {
                            license {
                                name = publication.licenseName
                                url = publication.licenseUrl
                            }
                        }
                        developers {
                            developer {
                                name = 'Dmitry Levshunov'
                                email = 'Levshunov.D.A@sberbank.ru'
                            }
                            developer {
                                name = 'Irina Karpenko'
                                email = 'IViKarpenko@sberbank.ru'
                            }
                        }
                        scm {
                            connection = publication.connectionUrl
                            developerConnection = publication.connectionUrl
                            url = publication.githubUrl
                        }
                    }

                    versionMapping {
                        allVariants {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenCvImpl
    }
}
