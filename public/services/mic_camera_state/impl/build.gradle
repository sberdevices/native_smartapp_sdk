plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

dependencies {
    api project(':public:services:mic_camera_state:api')

    implementation project(':public:services:mic_camera_state:aidl')

    implementation project(':public:common:binderhelper')
    implementation project(':public:common:binderhelper_api')
    implementation project(':public:common:logger')

    implementation "androidx.core:core-ktx:$versions.androidx.core"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinxCoroutines"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

afterEvaluate {
    publishing {
        repositories.add(rootProject.repositories.getByName('OSSRH'))

        publications {
            mavenMicCameraStateImpl(MavenPublication) {
                from components.release

                artifact androidSourcesJar

                groupId publication.pomGroupID
                artifactId "mic_camera_state"
                version publication.pomMicCameraStateVersion

                pom {
                    name = 'mic_camera_state'
                    description = 'mic-camera-state is utility library for getting microphone and camera state'
                    url = publication.githubUrl
                    licenses {
                        license {
                            name = publication.licenseName
                            url = publication.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            name = 'Igor Perminov'
                            email = 'Perminov.I.Yurye@sberbank.ru'
                        }
                        developer {
                            name = 'Nikolay Pahomov'
                            email = 'NMPakhomov@sberbank.ru'
                        }
                    }
                    scm {
                        connection = publication.connectionUrl
                        developerConnection = publication.developerConnection
                        url = publication.githubUrl
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenMicCameraStateImpl
    }
}
