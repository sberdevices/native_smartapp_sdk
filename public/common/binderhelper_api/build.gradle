plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

dependencies {
    implementation "androidx.annotation:annotation:$versions.annotation"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

afterEvaluate {
    publishing {
        repositories.add(rootProject.repositories.getByName('OSSRH'))

        publications {
            mavenBinderHelperApi(MavenPublication) {
                from components.release

                artifact androidSourcesJar

                groupId rootProject.ext.pomGroupID
                artifactId project.name
                version rootProject.ext.pomVersion

                pom {
                    name = project.name
                    description = 'binderhelper is utility library used to connect to servce'
                    url = publication.githubUrl
                    licenses {
                        license {
                            name = publication.licenseName
                            url = publication.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            name = 'Igor Perminov'
                            email = 'Perminov.I.Yurye@sberbank.ru'
                        }
                        developer {
                            name = 'Nikolay Pahomov'
                            email = 'NMPakhomov@sberbank.ru'
                        }
                    }
                    scm {
                        connection = publication.connectionUrl
                        developerConnection = publication.connectionUrl
                        url = publication.githubUrl
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenBinderHelperApi
    }
}